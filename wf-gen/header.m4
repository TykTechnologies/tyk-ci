define(xDATE, esyscmd(date -u))dnl
changequote(<<, >>)dnl
dnl xCOMPATIBILITY_NAME is the legacy directory that the code is installed in. This is also used for the package name. For new repos, use the repo name. The use of this parameter should reduce.
define(<<xCOMPATIBILITY_NAME>>, <<ifelse(xREPO, <<tyk>>, <<tyk-gateway>>, xREPO, <<tyk-analytics>>, <<tyk-dashboard>>, xREPO)>>)dnl
dnl The repository in Docker Hub defaults to xCOMPATIBILITY_NAME
define(<<xDH_REPO>>, <<ifelse(xREPO, <<tyk-pump>>, <<tyk-pump-docker-pub>>, xREPO, <<tyk-sink>>, <<tyk-mdcb-docker>>, xREPO, <<raava>>, <<developer-portal>>, xCOMPATIBILITY_NAME)>>)dnl
define(<<xPC_REPO>>, <<ifelse(xREPO, <<tyk-sink>>, <<tyk-mdcb>>, xCOMPATIBILITY_NAME)>>)dnl
define(<<xPC_PRIVATE>>, ifelse(xREPO, <<tyk-sink>>, <<1>>, <<0>>))dnl
define(<<xCGO>>, ifelse(xREPO, <<tyk>>, <<1>>, xREPO, <<tyk-analytics>>, <<1>>, xREPO, <<raava>>, <<1>>, <<0>>))dnl
define(<<xCONFIG_FILE>>, <<ifelse(xREPO, <<tyk-analytics>>, <<tyk_analytics.conf>>, xREPO, <<tyk-sink>>, <<tyk_sink.conf>>, xREPO, <<tyk-identity-broker>>, <<tib.conf>>, xREPO, <<tyk-pump>>, <<pump.conf>>, xREPO.conf)>>)dnl
dnl The version to test upgrades from, chosen to be the last versions before the wf-gen era, every repo should be explicity featured here
define(<<xUPGRADE_FROM>>, ifelse(xREPO, <<tyk>>, <<3.0.4>>, xREPO, <<tyk-analytics>>, <<3.0.4>>, xREPO, <<tyk-pump>>, <<1.2.0>>, xREPO, <<tyk-identity-broker>>, <<1.1.0>>, xREPO, <<tyk-sink>>, <<1.7.7>>, xREPO, <<raava>>, <<0.0.0>>))dnl
dnl The comments below are quoted so that they are not treated as comments
<<#>> Generated by: tyk-ci/wf-gen
<<#>> Generated on: xDATE
<<#>> Generation commands:
<<#>> xPR_CMD_LINE
<<#>> xM4_CMD_LINE
